plugins {
    id 'java'
}

group 'org.janelia.scicomp'
version '4.3.0'

sourceCompatibility = '10'
targetCompatibility = '10'

repositories {
    mavenCentral()
}

ext {
    jaxbVersion = '2.3.0'
    moduleName = 'org.janelia.tmog'
}

dependencies {
    implementation "com.sun.activation:javax.activation:1.2.0"
    implementation "javax.xml.bind:jaxb-api:" + jaxbVersion
    implementation "com.sun.xml.bind:jaxb-core:" + jaxbVersion
    runtime "com.sun.xml.bind:jaxb-impl:" + jaxbVersion
    
    implementation "commons-digester:commons-digester:1.8"
    implementation "commons-httpclient:commons-httpclient:3.1"
    implementation "commons-logging:commons-logging:1.1"
    implementation "com.google.code.gson:gson:2.7"
    implementation 'com.intellij:forms_rt:7.0.3'
    implementation "log4j:log4j:1.2.8"
    implementation "mysql:mysql-connector-java:5.1.22"
    implementation "net.java.dev.glazedlists:glazedlists_java15:1.8.0"
    implementation "org.jdesktop:swing-worker:1.1"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}


compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

// suggested by https://guides.gradle.org/building-java-9-modules/ but breaks build ...

//test {
//    inputs.property("moduleName", moduleName)
//    doFirst {
//        jvmArgs = [
//                '--module-path', classpath.asPath,
//                '--add-modules', 'ALL-MODULE-PATH',
//                '--add-reads', "$moduleName=junit",
//                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
//        ]
//        classpath = files()
//    }
//}

// stolen from https://stackoverflow.com/questions/3963708/gradle-how-to-display-test-results-in-the-console-in-real-time
tasks.withType(Test) {
    testLogging {
        events "failed" // "passed", "skipped"
        showStandardStreams true
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

Map<String, ?> manifestAttributes = [
        'Application-Name': 'Janelia Transmogrifier',
        'Automatic-Module-Name': moduleName,
        'Implementation-Title': 'org.janelia.it.ims.tmog',
        'Implementation-Version': version,
        'Main-Class': 'org.janelia.it.ims.tmog.JaneliaTransmogrifier'
]

jar {
    classifier='dev'

    manifest {
        attributes(manifestAttributes)
    }

    doLast {
        println 'created ' + archivePath
    }
}

task prodJar(type: Jar) {

    def dbSageKeys = ["db.sage.url", "db.sage.password"]
    def dbSagePropsFile = file(System.getProperty("user.home") + '/ssl/code-cert/2018/sage.properties')
    Properties prodSageProps

    doFirst {
        prodSageProps = loadProperties(dbSageKeys, dbSagePropsFile)
    }

    // remove lab specific config files from deployed jar
    exclude('**/transmogrifier_config_*.xml')

    // overwrite dev environment configuration with production values
    filesMatching('**/sage.properties') {
        filter {
            replacePropertiesInLine(dbSageKeys, prodSageProps, it)
        }
    }

    filesMatching('**/log4j.xml') {
        filter {
            it.replace('ref="SYSLOG_STUB', 'ref="SYSLOG')
        }
    }

    classifier='prod'

    manifest {
        attributes(manifestAttributes)
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    doLast {
        println 'created ' + archivePath
    }
}

// include prodJar task as part of assemble lifecycle task
assemble.dependsOn(prodJar)

task logUsefulBuildInfo {
    doLast {
        println "\nbuilt the following jar files:"
        file(project.libsDir).eachFileMatch(~/.*.jar/) { file ->
            println file.getAbsolutePath()
        }
    }
}

build.dependsOn(logUsefulBuildInfo)

static Properties loadProperties(List<String> keys,
                                 File propsFile) {
    def props = new Properties()
    propsFile.withInputStream { props.load(it) }
    keys.each {
        if (! props.containsKey(it)) {
            def message = "required key '" + it + "' is missing in " + propsFile.getAbsolutePath()
            throw new GradleScriptException(message, null)
        }
    }
    println "loaded properties from " + propsFile.getAbsolutePath()
    return props
}

static String replacePropertiesInLine(List<String> keys,
                                      Properties props,
                                      String line) {
    def replacedLine = line
    keys.each {
        if (line.startsWith(it)) {
            replacedLine = it + "=" + props.getProperty(it)
            // println "replaced '" + it + "' value"
        }
    }
    return replacedLine
}